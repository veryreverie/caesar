! ======================================================================
! The abstract potential type.
! ======================================================================
! See the example module in potential_pointer.f90 for how to use this type.
module potential_module
  use common_module
  
  use anharmonic_data_module
  implicit none
  
  private
  
  public :: PotentialData
  public :: WriteLambda
  public :: ReadLambda
  
  type, abstract, extends(Stringsable) :: PotentialData
  contains
    ! Given all input data, generate the set of sampling points at which
    !    electronic structure calculations should be run in order to
    !    map out the potential.
    procedure(generate_sampling_points_PotentialData), public, deferred :: &
       & generate_sampling_points
    
    ! Given the same data as above, generate the potential.
    ! All electronic structure calculations generated by
    !    generate_sampling_points will all have been run.
    procedure(generate_potential_PotentialData), public, deferred :: &
       & generate_potential
    
    ! Return the energy and force at a given real or complex displacement.
    generic, public :: energy => energy_RealModeDisplacement, &
                               & energy_ComplexModeDisplacement
    generic, public :: force  => force_RealModeDisplacement,  &
                               & force_ComplexModeDisplacement
    procedure(energy_RealModeDisplacement_PotentialData), public, &
       & deferred :: energy_RealModeDisplacement
    procedure(energy_ComplexModeDisplacement_PotentialData), public, &
       & deferred :: energy_ComplexModeDisplacement
    procedure(force_RealModeDisplacement_PotentialData), public, &
       & deferred :: force_RealModeDisplacement
    procedure(force_ComplexModeDisplacement_PotentialData), public, &
       & deferred :: force_ComplexModeDisplacement
  end type
  
  interface
    ! Takes a structure and a directory name,
    !    makes the directory and sets up an electronic structure calculation
    !    based on the structure.
    ! Function implementation defined in run_anharmonic.f90.
    subroutine WriteLambda(structure,directory)
      import StructureData
      import String
      implicit none
      
      type(StructureData), intent(in) :: structure
      type(String),        intent(in) :: directory
    end subroutine
    
    ! Reads the results of the electronic structure calculation from the
    !    given directory.
    ! Function implementation defined in calculate_potential.f90.
    function ReadLambda(directory) result(output)
      import String
      import ElectronicStructure
      implicit none
      
      type(String), intent(in)  :: directory
      type(ElectronicStructure) :: output
    end function
  end interface
  
  abstract interface
    subroutine generate_sampling_points_PotentialData(this,inputs, &
       & sampling_points_dir,logfile,write_lambda)
      import PotentialData
      import AnharmonicData
      import String
      import OFile
      import WriteLambda
      import StructureData
      implicit none
      
      class(PotentialData), intent(inout) :: this
      type(AnharmonicData), intent(in)    :: inputs
      type(String),         intent(in)    :: sampling_points_dir
      type(OFile),          intent(inout) :: logfile
      procedure(WriteLambda)              :: write_lambda
    end subroutine
    
    subroutine generate_potential_PotentialData(this,inputs, &
       & weighted_energy_force_ratio,sampling_points_dir,logfile,read_lambda)
      import dp
      import PotentialData
      import AnharmonicData
      import String
      import OFile
      import ReadLambda
      import ElectronicStructure
      implicit none
      
      class(PotentialData), intent(inout) :: this
      type(AnharmonicData), intent(in)    :: inputs
      real(dp),             intent(in)    :: weighted_energy_force_ratio
      type(String),         intent(in)    :: sampling_points_dir
      type(OFile),          intent(inout) :: logfile
      procedure(ReadLambda)               :: read_lambda
    end subroutine
    
    impure elemental function energy_RealModeDisplacement_PotentialData(this, &
       & displacement) result(output)
      import dp
      import PotentialData
      import RealModeDisplacement
      implicit none
      
      class(PotentialData),       intent(in) :: this
      type(RealModeDisplacement), intent(in) :: displacement
      real(dp)                               :: output
    end function
    
    impure elemental function energy_ComplexModeDisplacement_PotentialData( &
       & this,displacement) result(output)
      import dp
      import PotentialData
      import ComplexModeDisplacement
      implicit none
      
      class(PotentialData),          intent(in) :: this
      type(ComplexModeDisplacement), intent(in) :: displacement
      complex(dp)                               :: output
    end function
    
    impure elemental function force_RealModeDisplacement_PotentialData(this, &
       & displacement) result(output)
      import PotentialData
      import RealModeDisplacement
      import RealModeForce
      implicit none
      
      class(PotentialData),       intent(in) :: this
      type(RealModeDisplacement), intent(in) :: displacement
      type(RealModeForce)                    :: output
    end function
    
    impure elemental function force_ComplexModeDisplacement_PotentialData( &
       & this,displacement) result(output)
      import PotentialData
      import ComplexModeDisplacement
      import ComplexModeForce
      implicit none
      
      class(PotentialData),          intent(in) :: this
      type(ComplexModeDisplacement), intent(in) :: displacement
      type(ComplexModeForce)                    :: output
    end function
  end interface
contains
end module
